apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/Leaking/Hunter")
            credentials {
                username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
            }
        }
        maven {
            name = 'CustomMavenRepo'
            url = "file://${rootDir}/repo"
        }
    }
    publications {
        hunter_transform(MavenPublication) {
            groupId rootProject.ext.LIB_GROUP_ID
            artifactId rootProject.ext.LIB_ARTIFACT_ID
            version rootProject.ext.LIB_VERSION
            artifact("$buildDir/outputs/aar//${rootProject.ext.LIB_ARTIFACT_ID}-release.aar")
            artifact(sourceJar)
        }
    }
}
